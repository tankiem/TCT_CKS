@echo off
setlocal

set "DesktopPath=%UserProfile%\Desktop"
set "ZipFilePath=%DesktopPath%\VGCAv1_Installer.zip"
set "ExtractPath=%DesktopPath%\"
set "DownloadUrl=https://drive.usercontent.google.com/download?id=1qDK2dEzJRQ8VAsHgfX-hiS7x3VHIZy0Z&export=download&authuser=0&confirm=t&uuid=450a949d-0fca-44d8-a283-c14c24b25fb2&at=AEz70l6M64YHPEZ0aZ6UXh4_-gL3:1741491402578"

echo Dang tai tep...
curl "%DownloadUrl%" --output "%ZipFilePath%"

if errorlevel 1 (
    echo Khong the tai tep.
    pause
    exit /b 1
)

echo Dang giai nen...
powershell -Command "Expand-Archive -Path '%ZipFilePath%' -DestinationPath '%ExtractPath%' -Force"

if errorlevel 1 (
    echo Khong the giai nen.
    pause
    exit /b 1
)

echo Dang cai dat phan mem quan ly token VGCA...
for /r "%ExtractPath%" %%i in (VGCA*.exe) do (
    set "ExePath=%%i" 
    goto :run 
) 


echo Khong tim thay chuong trinh cai dat.
pause
exit /b 1

:run
"%ExePath%" /S
echo Da cai xong phan mem.

echo Dang xoa file cai dat...
del "%ZipFilePath%"
del "%ExePath%"
if errorlevel 1 (
    echo Khong the xoa file...
    pause
    exit /b 1
)

echo Da xoa file cai dat.

endlocal
pause

@echo off
setlocal

set "DesktopPath=%UserProfile%\Desktop"
set "ZipFilePath=%DesktopPath%\VNPT-CA Plugin_Office_Setup_1.0.4.6.zip"
set "ExtractPath=%DesktopPath%\"
set "DownloadUrl=https://drive.usercontent.google.com/download?id=1RuboDNFYbLeOp_7v0KQU-8DP7Nuq_pGU&export=download&authuser=0&confirm=t&uuid=ba67145b-e1fc-4176-a790-2b0a3ae7171c&at=AEz70l4m0V24eop6BAdFG4FNEepR:1741443148263"

echo Dang tai tep...
curl "%DownloadUrl%" --output "%ZipFilePath%"

if errorlevel 1 (
    echo Khong the tai tep.
    pause
    exit /b 1
)

echo Dang giai nen...
powershell -Command "Expand-Archive -Path '%ZipFilePath%' -DestinationPath '%ExtractPath%' -Force"

if errorlevel 1 (
    echo Khong the giai nen.
    pause
    exit /b 1
)

echo Dang cai dat Plugin VNPT...
for /r "%ExtractPath%" %%i in (VNPT*.exe) do (
    set "ExePath=%%i" 
    goto :run 
) 


echo Khong tim thay chuong trinh cai dat.
pause
exit /b 1

:run
"%ExePath%" /SILENT /FORCECLOSEAPPLICATIONS
echo Da cai xong Plugin VNPT.

echo Dang xoa file cai dat...
del "%ZipFilePath%"
del "%ExePath%"
if errorlevel 1 (
    echo Khong the xoa file...
    pause
    exit /b 1
)

echo Da xoa file cai dat.

endlocal
pause
